group 'com.github.brunomndantas'
version '1.0.0'

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'

sourceCompatibility = 1.8

test {
    // show standard out and standard error of the test JVM(s) on the console
    testLogging.showStandardStreams = true
}

repositories {
    mavenCentral()
}

dependencies {
    //JUnit
    testCompile group: 'junit', name: 'junit', version: '4.12'

    //Apache Commons
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.8'
    compile group: 'org.apache.commons', name: 'commons-text', version: '1.4'

    //LOG4J
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.1'

    //Selenium
    compile('org.seleniumhq.selenium:selenium-java:3.4.0') {
        exclude group: 'org.seleniumhq.selenium', module: 'selenium-android-driver'
        exclude group: 'org.seleniumhq.selenium', module: 'selenium-iphone-driver'
        exclude group: 'org.seleniumhq.selenium', module: 'selenium-safari-driver'
        exclude group: 'org.webbitserver', module: 'webbit'
        exclude group: 'commons-codec', module: 'commons-codec'
        exclude group: 'cglib', module: 'cglib-nodep'
    }
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId 'com.github.brunomndantas'
            artifactId 'jscrapper'
            version '1.0.0'

            from components.java

            // create the sign pom artifact
            pom.withXml {
                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
            }

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

            // create the signed artifacts
            project.tasks.signArchives.signatureFiles.each {
                artifact(it) {
                    def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
                    if (matcher.find()) {
                        classifier = matcher.group(1)
                    } else {
                        classifier = null
                    }
                    extension = 'jar.asc'
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // eliminate test-scoped dependencies (no need in maven central POMs)
        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description 'Web Scrapper for dynamic pages'
            name 'JScrapper'
            url 'https://github.com/BrunoMNDantas/JScrapper'
            organization {
                name 'com.github.brunomndantas'
                url 'https://github.com/BrunoMNDantas'
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/BrunoMNDantas/JScrapper/issues'
            }
            licenses {
                license {
                    name 'GNU General Public License v3.0'
                    url 'https://github.com/BrunoMNDantas/JScrapper/blob/master/LICENSE'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/BrunoMNDantas/JScrapper'
                connection 'scm:git:git://github.com/BrunoMNDantas/JScrapper.git'
                developerConnection 'scm:git:ssh://git@github.com:BrunoMNDantas/JScrapper.git'
            }
            developers {
                developer {
                    name 'Bruno Dantas'
                }
            }
        }

        //evil unnecessary complicated change to fix packaging type in the pom
        def children = root.children()
        def versionIndex = children.indexOf(children.find {it.name().localPart == 'packaging'})
        children.remove(versionIndex)
    }
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
    tasks.publishMavenJavaPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenJavaPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
}